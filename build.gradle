plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group = 'org.fastmcmirror'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://repo.clojars.org'
        name 'Clojars'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    implementation 'com.j256.ormlite:ormlite-core:6.1'
    implementation 'com.j256.ormlite:ormlite-jdbc:6.1'
    implementation 'hikari-cp:hikari-cp:2.14.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'org.xerial:sqlite-jdbc:3.39.3.0'
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.2'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    relocate("com.j256.ormlite", "org.fastmcmirror.bartereco.ormlite")
    relocate("com.zaxxer.hikari", "org.fastmcmirror.bartereco.hikari")
    relocate("com.google.gson", "org.fastmcmirror.bartereco.gson")
    archiveName("BarterEconomy.jar")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "org.fastmcmirror"
            artifactId "BarterEconomy"
            version this.version
            artifact shadowJar
        }
    }
    repositories {
        maven {
            url = "https://repo.fastmcmirror.org/content/repositories/releases/"
            credentials {
                username System.getenv("USERNAME")
                password System.getenv("PASSWORD")
            }
        }
    }
}